/*
 * Copyright 2020 wcaokaze
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.4.0'
    ext.coroutines_version = '1.3.9'
    ext.serialization_version = '1.0.0-RC'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = [
                    "-Xinline-classes",
                    "-Xuse-experimental=kotlin.Experimental",
                    "-Xuse-experimental=kotlin.contracts.ExperimentalContracts"
            ]
        }
    }

    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute module("com.wcaokaze:cac7er:0.8.0") with project(':Cac7er')
            substitute module("com.wcaokaze:cac7er-vue:0.8.0") with project(':Cac7er-Vue')
            substitute module("com.wcaokaze.koshian:koshian-core:0.6.0") with project(':koshian-core')
            substitute module("com.wcaokaze.koshian:koshian-androidx:0.6.0") with project(':koshian-androidx')
            substitute module("com.wcaokaze.koshian:koshian-recyclerview:0.6.0") with project(':koshian-recyclerview')
            substitute module("com.wcaokaze.vue.android:vue-android-core:0.2.0") with project(':vue-android-core')
            substitute module("com.wcaokaze.vue.android:vue-android-stream:0.2.0") with project(':vue-android-stream')
            substitute module("com.wcaokaze.vue.android:vue-android-androidx:0.2.0") with project(':vue-android-androidx')
            substitute module("com.wcaokaze.vue.android:vue-android-koshian:0.2.0") with project(':vue-android-koshian')
            substitute module("com.wcaokaze.vue.android:vue-android-koshian-recyclerview:0.2.0") with project(':vue-android-koshian-recyclerview')
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}